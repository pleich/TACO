/*inserted error:  12: GradeC -> kcn ------> 12: GradeC -> kcz
/*inserted error:  17: GradeC -> kcn ------>17: GradeC -> kcz
/*error spec agreement:  (l0 == 0) -> [](cz == 0);

/*This is the same as kingsCons-compressed.ta however without the b shared variable. The idea is that we can use k and make sure that at least the last iteration is not byzantyne */
skel Proc {
/*more details about these variables can be found in the image: kingconsdetails.jpg*/
shared xw,yw,xg,yg,xk,yk,xyzg,k,d;/*x_w,y_w,x_g,y_g,z_g,x_k,y_k, xyzg = xg + yg + zg*/
parameters N, T, F,Fwz,Fwn,Fgz,Fgn;/*Fwz=F_w^0,Fwn=F_w^1,Fgz=F_g^0,Fgn=F_g^1*/
/*Explanation of some guards*/
/*All processes voted: xyzg >= N - F*/
/* 0s more than ones: ( yg < 1 && xg >= 1 ) || ( yg < N - T - Fgn && xg >= N - T - Fgz ) || ( yg >= N - T - Fgn && xg >= N - T - Fgz ) */ 
/* 1s more than zeros: ( yg >= 1 && xg < 1 ) || ( yg >= N - T - Fgn && xg < N - T - Fgz ) || ( yg >= N - T - Fgn && xg >= N - T - Fgz ) */ 
/*grade 1 when more 0s: xg >= N - T - Fgz */
/*grade 0 when more 0s: xg < N - T - Fgz*/
/*grade 1 when more 1s: yg >= N - T - Fgn*/
/*grade 0 when more 1s: yg < N - T - Fgn*/
/*F >= 1*/
/*var ordering: */
assumptions (1) {
    N >= 1;
    T >= F;
    N >= 3 * T + 1;
    F >= Fwz + Fwn;
    T >= Fwz + 1; /*to make sure that T - Fwz + 1 > 1*/
    T >= Fwn + 1; /*to make sure that T - Fwn + 1 > 1*/
    F >= Fgz + Fgn;
    F >= 1;
 }

locations (0) {
    l0: [0]; 
    l1: [1];
    WeakC: [2]; 
    GradeC:[3];
    kkcz:[4];
    kkcn:[5];    
    kcz:[6];
    kcn:[7];
    cz:[8];
    cn:[9];
  }

  inits (0) {
    (l0 + l1) == N - F;
    WeakC==0; 
    GradeC==0;
    kkcz==0;
    kkcn==0;    
    kcz==0;
    kcn==0;
    cz==0;
    cn==0;
    l0 == 0;
    xw == 0;
    yw == 0;
    xg == 0;
    yg == 0;
    xk == 0;
    yk == 0;
    xyzg == 0;
    k == 0;
    d == 0;
  }

  rules (0) {
 
  0: l0 -> WeakC
      when (true)
      do { xw' == xw + 1; };
  1: l1 -> WeakC
      when (true)
      do { yw' == yw + 1; };
  2: WeakC -> GradeC 
      when (xw >= N - T - Fwz)
      do { xg' == xg + 1; xyzg' == xyzg + 1; };
  3: WeakC -> GradeC 
      when (yw >= N - T - Fwn)
      do { yg' == yg + 1; xyzg' == xyzg + 1; };
  4: WeakC -> GradeC
      when (xw >= T - Fwz + 1 && yw >= T - Fwn + 1)
      do { xyzg' == xyzg + 1; };
  /* King */
  5: GradeC -> kkcz
      when (xyzg >= N - F && yg < 1 && xg >= 1 && d < 1)/*all processes voted and king vote is 0*/
      do { xk' == xk + 1;  k' == k + 1; d' == d + 1 };
  6: GradeC -> kkcz
      when (xyzg >= N - F && yg < N - T - Fgn && xg >= N - T - Fgz && d < 1)/*all processes voted and king vote is 0*/
      do { xk' == xk + 1;  k' == k + 1; d' == d + 1 }; 
  7: GradeC -> kkcn
      when (xyzg >= N - F && yg >= 1 && xg < 1 && d < 1)/*all processes voted and king vote is 1*/
      do { yk' == yk + 1; k' == k + 1; d' == d + 1 };
  8: GradeC -> kkcn
      when (xyzg >= N - F && yg >= N - T - Fgn && xg < N - T - Fgz && d < 1)/*all processes voted and king vote is 1*/
      do { yk' == yk + 1; k' == k + 1; d' == d + 1 };
  /* non king */
  9: GradeC -> kcz /*t_9: All processes voted, 0more1, grade 1, d>=1(king chosen)*/
      when (xyzg >= N - F && yg < N - T - Fgn && xg >= N - T - Fgz && d >= 1)
      do {d' == d + 1};              
  10: GradeC -> kcz /*t_10: All pr voted, (0more1 or 1more0), grade 0, d>=1(king chosen), xk >= 1(king voted 0)*/
      when (xyzg >= N - F && yg < N - T - Fgn  && xg < N - T - Fgz && d >= 1 && xk >= 1)
      do {d' == d + 1};
  11: GradeC -> kcz /*t_12: All pr voted, (0more1 or 1more0), grade 0, d>=1(king chosen), xk < 1 && yk<1(byzantine king) we go non-deterministically either to kcz or to kcn*/
       when (xyzg >= N - F && yg < N - T - Fgn && xg < N - T - Fgz && d >= 1 && xk <1 && yk <1)
      do {d' == d + 1};
  12: GradeC -> kcz /*t_14: All processes voted, 1more0, grade 1, d>=1(king chosen)*/
      when (xyzg >= N - F && xg < N - T - Fgz && yg >= N - T - Fgn && d >= 1)
      do {d' == d + 1};
  13: GradeC -> kcn /*t_15: All pr voted, 1more0, grade 0, d>=1(king chosen), yk >= 1(king voted 1)*/
      when (xyzg >= N - F && xg < N - T - Fgz && yg < N - T - Fgn && d >= 1 && yk >= 1)
      do {d' == d + 1};
  14: GradeC -> kcn /*t_17: All pr voted, (1more0 or 0more1), grade 0, d>=1(king chosen), xk < 1 && yk<1(byzantine king) we go non-deterministically either to kcz or to kcn*/
      when (xyzg >= N - F && xg < N - T - Fgz && yg < N - T - Fgn && d >= 1 && xk <1 && yk <1)
      do {d' == d + 1};
 
 /* Byzantine King d < 1 && k< F+1 since a byzantine king can be selected at most F times*/ 
 
  15: GradeC -> kcz /*rep-t_9: All processes voted, 0more1, grade 1, d<1(no king)*/
      when (xyzg >= N - F && yg < N - T - Fgn && xg >= N - T - Fgz && d < 1 && k < F + 1)
      do {d' == d + 1; k' == k + 1;};
  16: GradeC -> kcz /*rep-t_11: All pr voted, 1more0, grade 0, d<1(no king) ======> non deterministic*/
      when (xyzg >= N - F && xg < N - T - Fgz && yg < N - T - Fgn && d < 1 && k < F + 1)
      do {d' == d + 1; k' == k + 1};
  17: GradeC -> kcz /*rep-t_14: All processes voted, 1more0, grade 1, d<1(no king)*/
      when (xyzg >= N - F && xg < N - T - Fgz && yg >= N - T - Fgn && d < 1 && k < F + 1)
      do {d' == d + 1; k' == k + 1};
  18: GradeC -> kcn /*rep-t_15: All pr voted, 1more0, grade 0, d<1(no king) ======> non deterministic*/
      when (xyzg >= N - F && xg < N - T - Fgz && yg < N - T - Fgn && d < 1 && k < F + 1)
      do {d' == d + 1; k' == k + 1};
 
  /* end of Byzantine king*/
  19: kkcz -> cz
     when (k >= T + 2)
     do {}; 
  20: kkcn -> cn
     when (k >= T + 2) 
     do {};
  21: kkcz -> l0
     when (k < T + 2 && d >= N - F)
     do { xw'==0;yw'==0;xg'==0;yg'==0;xyzg'==0;xk'==0;yk'==0;d'==0;};/*resets*/
  22: kkcn -> l1
     when (k < T + 2 && d >= N - F)
     do { xw'==0;yw'==0;xg'==0;yg'==0;xyzg'==0;xk'==0;yk'==0;d'==0;};/*resets*/
  23: kcz -> cz
     when (k >= T + 2) 
     do {};
  24: kcn -> cn
     when (k >= T + 2) 
     do {};
  25: kcz -> l0
     when (k < T + 2 && d < 1) 
     do {};
  26: kcn -> l1
     when (k < T + 2 && d < 1) 
     do {};
  }

  specifications (1) {
        reachCons: [](cz == 0);
  }

} /* Proc */
